/*
Chapter 12: Processing Data Using Hash Object
	Topics:
		○ Declaring Hash Objects (No examples)
		○ Defining Hash Objects
		○ Finding Key Values in a Hash Object
		○ Writing a Hash Object to a Table
		○ Hash Object Processing
		○ Using Hash Iterator Objects
		○ Quiz

*/

LIBNAME certadv '/home/u45038867/certadv';

/*******************************************
* Defining Hash Objects                    *
*                                          *
* This example covers a few of the         * 
* Sections/topics covered in this chapter  *
********************************************/

*Example Defining a hash object;
DATA work.report;
	IF 0 THEN	
		SET certadv.ctcities (keep=code city name);
	IF _N_=1 THEN DO;
		
		*creating a hash object named airports based on .ctcities dataset;
		DECLARE hash airports (dataset: "certadv.ctcities");
		airports.definekey ("Code"); *Code is the key component;
		airports.definedata ("City", "Name");*Defining data components;
		airports.definedone();
		
		*adding a key and data to the hash object;
		rc=airports.add(key:'123', data:"Minot", data:"MINOT International");
		putlog "The value of rc is: " rc=;
		rc=airports.add(key:'124', data:"Bismarck", data:"Bismarck International");
		
		*This creates a dataset that contains the data components of the hash object;
		*The key values are not included in the output table;
		rc=airports.output(dataset:'afteradd');
		
	END;
RUN;

PROC PRINT data=afteradd;
RUN;




/*************************
* Hash Object Processing *
**************************/
DATA work.countrycode;
	DROP rc;
	length ContinentName $30;
	
	*On first iteration, we set the hash object;
	IF _n_=1 THEN DO;
	
		*Turning off message about uninitialized variable ContinentName;
		CALL MISSING (ContinentName);
		
		*Creating, defining key and data components of hash object;
		DECLARE hash ContName();
		ContName.definekey('ContinentID');
		ContName.definedata('ContinentName');
		ContName.definedone();
		
		*Adding key and data pairs to hash object;
		ContName.add(key:91, data: 'North America');
		ContName.add(key:93, data: 'Europe');
		ContName.add(key:94, data: 'Africa');
		ContName.add(key:95, data: 'Asia');
		ContName.add(key:96, data: 'Australia/Pacific');
	END;
	
	*Reading in and setting values of work.country code;
	SET certadv.country (keep=ContinentID Country CountryName);
	
	*using the continentID key value to search the hash object;
	*returns the ContinentName (data component) to the PDV;
	rc=ContName.find();
RUN;

PROC PRINT data=work.countrycode noobs;
RUN;
